buildscript {
    // Make sure to use the same AGP as the main project.
    def androidGradlePlugin = '4.0.1'
    /**
     * Calculates the right lint version for the provided [androidGradlePlugin] version. AGP is
     * tightly coupled with lint because it implements LintClient. The formula to calculate the
     * lint version relative to AGP is: AGP version + 23.
     *
     * E.g. AGP version 3.2.1 -> Lint version 26.2.1
     */
    def lintVersion = { agp ->
        def (majorVersion, minorVersion, patchVersion) = agp.split("\\.")
        int lintMajorVersion = (majorVersion as Integer) + 23
        return "$lintMajorVersion.$minorVersion.$patchVersion"
    }
    ext.versions = [
        androidGradlePlugin     : androidGradlePlugin,
        androidLint             : lintVersion(androidGradlePlugin),
        compileSdk              : 30,
        minSdk                  : 14,
        targetSdk               : 30,
        kotlin                  : '1.4.0',
        kotlinCoroutines        : '1.3.9',
        kotlinMetadataJvm       : '0.1.0',
        androidxAppcompat       : '1.2.0',
        androidxConstraintlayout: '2.0.1',
        androidxFragment        : '1.2.5',
        androidxLifecycle       : '2.2.0',
        androidxNavigation      : '2.3.0',
        androidxRoom            : '2.2.5',
        androidxTestCore        : '1.3.0',
        androidxTestRunner      : '1.3.0',
        androidxTestJunit       : '1.1.2',
        androidxTestEspresso    : '3.3.0',
        autoService             : '1.0-rc7',
        okhttp                  : '3.12.12',
        dagger                  : '2.29',
        kotlinPoet              : '1.6.0',
        thrift                  : '0.12.0',
        mockito                 : '2.28.2',
        mockitoKotlin           : '2.2.0',
        mockk                   : '1.10.0',
        robolectric             : '4.4',
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    }
}

allprojects {
    group = 'com.linecorp.lich'
    repositories {
        google()
        jcenter()
    }
    afterEvaluate {
        if (ext.has('publicationType')) {
            afterEvaluate {
                configurePublication(project, ext.publicationType)
            }
        }
    }
}

def configurePublication(project, type) {
    configure(project) {
        switch (type) {
            case 'jar':
                task sourcesJar(type: Jar) {
                    archiveClassifier = 'sources'
                    from sourceSets.main.allSource
                }
                publishing {
                    publications {
                        myPublication(MavenPublication) {
                            from components.java
                            artifact sourcesJar
                            pom.licenses {
                                license {
                                    name = 'Apache-2.0'
                                    url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                                    distribution = 'repo'
                                }
                            }
                        }
                    }
                }
                break
            case 'aar':
                task sourcesJar(type: Jar) {
                    archiveClassifier = 'sources'
                    from android.sourceSets.main.java.srcDirs
                }
                publishing {
                    publications {
                        myPublication(MavenPublication) {
                            from components.release
                            artifact sourcesJar
                            pom.licenses {
                                license {
                                    name = 'Apache-2.0'
                                    url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                                    distribution = 'repo'
                                }
                            }
                        }
                    }
                }
                break
            default:
                throw new InvalidUserDataException("Unknown publicationType: $type")
        }
        publishing {
            repositories {
                maven {
                    url = findProperty('publishingRepositoryUrl') ?: "$rootDir/repo"
                }
            }
        }
        bintray {
            user = findProperty('bintrayUser') ?: ''
            key = findProperty('bintrayApiKey') ?: ''
            publications = ['myPublication']
            publish = true
            pkg {
                userOrg = 'line'
                repo = 'lich'
                name = project.name
                websiteUrl = 'https://github.com/line/lich'
                issueTrackerUrl = 'https://github.com/line/lich/issues'
                vcsUrl = 'https://github.com/line/lich.git'
                licenses = ['Apache-2.0']
                publicDownloadNumbers = true
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task cleanRepo(type: Delete) {
    delete "$rootDir/repo"
}
