buildscript {
    // Make sure to use the same AGP as the main project.
    def androidGradlePlugin = '7.0.4'
    /**
     * Calculates the right lint version for the provided AGP version. AGP is tightly coupled with
     * Android Lint because it implements LintClient. The formula to calculate the lint version
     * relative to AGP is: AGP version + 23.
     *
     * E.g. AGP version 4.2.1 -> Android Lint version 27.2.1
     */
    def androidLintVersion = { agpVersion ->
        def (majorVersion, minorAndPatchVersion) = agpVersion.split("\\.", 2)
        int lintMajorVersion = (majorVersion as Integer) + 23
        return "$lintMajorVersion.$minorAndPatchVersion"
    }
    ext.versions = [
        androidGradlePlugin     : androidGradlePlugin,
        androidLint             : androidLintVersion(androidGradlePlugin),
        compileSdk              : 31,
        minSdk                  : 14,
        targetSdk               : 31,
        kotlin                  : '1.6.10',
        kotlinCoroutines        : '1.6.0',
        kotlinMetadataJvm       : '0.4.1',
        ksp                     : '1.6.10-1.0.2',
        androidxAppcompat       : '1.4.1',
        androidxConstraintlayout: '2.1.3',
        androidxFragment        : '1.4.0',
        androidxLifecycle       : '2.4.0',
        androidxNavigation      : '2.3.5',
        androidxRoom            : '2.4.1',
        androidxTestCore        : '1.4.0',
        androidxTestRunner      : '1.4.0',
        androidxTestJunit       : '1.1.3',
        androidxTestEspresso    : '3.4.0',
        androidMaterial         : '1.5.0',
        autoService             : '1.0.1',
        autoServiceKsp          : '1.0.0',
        okhttp                  : '4.9.3',
        kotlinPoet              : '1.10.2',
        thrift                  : '0.13.0',
        dexmaker                : '2.28.1',
        mockito                 : '4.2.0',
        mockitoKotlin           : '4.0.0',
        mockk                   : '1.12.0',
        robolectric             : '4.7.3',
    ]
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.google.devtools.ksp:symbol-processing-gradle-plugin:${versions.ksp}"
    }
}

plugins {
    id 'io.codearte.nexus-staging' version '0.30.0'
}

nexusStaging {
    packageGroup = 'com.linecorp'
    stagingProfileId = '465b0f4eecd582'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
