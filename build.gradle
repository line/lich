buildscript {
    // Make sure to use the same AGP as the main project.
    def androidGradlePlugin = '4.2.1'
    /**
     * Calculates the right lint version for the provided [androidGradlePlugin] version. AGP is
     * tightly coupled with lint because it implements LintClient. The formula to calculate the
     * lint version relative to AGP is: AGP version + 23.
     *
     * E.g. AGP version 3.2.1 -> Lint version 26.2.1
     */
    def lintVersion = { agp ->
        def (majorVersion, minorVersion, patchVersion) = agp.split("\\.")
        int lintMajorVersion = (majorVersion as Integer) + 23
        return "$lintMajorVersion.$minorVersion.$patchVersion"
    }
    ext.versions = [
        androidGradlePlugin     : androidGradlePlugin,
        androidLint             : lintVersion(androidGradlePlugin),
        compileSdk              : 30,
        minSdk                  : 14,
        targetSdk               : 30,
        kotlin                  : '1.5.10',
        kotlinCoroutines        : '1.5.0',
        kotlinMetadataJvm       : '0.3.0',
        ksp                     : '1.5.10-1.0.0-beta01',
        androidxAppcompat       : '1.3.0',
        androidxConstraintlayout: '2.0.4',
        androidxFragment        : '1.3.4',
        androidxLifecycle       : '2.3.1',
        androidxNavigation      : '2.3.5',
        androidxRoom            : '2.3.0',
        androidxTestCore        : '1.3.0',
        androidxTestRunner      : '1.3.0',
        androidxTestJunit       : '1.1.2',
        androidxTestEspresso    : '3.3.0',
        autoService             : '1.0',
        okhttp                  : '4.9.1',
        dagger                  : '2.36',
        kotlinPoet              : '1.8.0',
        thrift                  : '0.13.0',
        dexmaker                : '2.28.1',
        mockito                 : '3.10.0',
        mockitoKotlin           : '2.2.0',
        mockk                   : '1.11.0',
        robolectric             : '4.5.1',
        dokka                   : '1.4.32',
    ]
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.google.devtools.ksp:com.google.devtools.ksp.gradle.plugin:${versions.ksp}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.30.0"
    }
}

apply plugin: 'io.codearte.nexus-staging'

allprojects {
    group = 'com.linecorp.lich'
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    afterEvaluate {
        if (ext.has('publication')) {
            afterEvaluate {
                configurePublication(project, ext.publication)
            }
        }
    }
}

def configurePublication(project, publication) {
    configure(project) {
        switch (publication.type) {
            case 'jar':
                task sourcesJar(type: Jar) {
                    archiveClassifier = 'sources'
                    from sourceSets.main.allSource
                }
                task javadocJar(type: Jar) {
                    archiveClassifier = 'javadoc'
                    from dokkaJavadoc
                }
                publishing {
                    publications {
                        maven(MavenPublication) {
                            from components.java
                            artifact sourcesJar
                            artifact javadocJar
                        }
                    }
                }
                break
            case 'aar':
                task sourcesJar(type: Jar) {
                    archiveClassifier = 'sources'
                    from android.sourceSets.main.java.srcDirs
                }
                task javadocJar(type: Jar) {
                    archiveClassifier = 'javadoc'
                    from dokkaJavadoc
                }
                publishing {
                    publications {
                        maven(MavenPublication) {
                            from components.release
                            artifact sourcesJar
                            artifact javadocJar
                        }
                    }
                }
                break
            default:
                throw new InvalidUserDataException("Unknown publication type: $publication.type")
        }
        publishing {
            publications {
                maven(MavenPublication) {
                    pom {
                        name = publication.name
                        description = publication.description
                        url = publication.url
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution = 'repo'
                            }
                        }
                        developers {
                            developer {
                                name = 'LINE Corporation'
                                email = 'dl_oss_dev@linecorp.com'
                                url = 'https://engineering.linecorp.com/en/'
                            }
                        }
                        scm {
                            connection = 'scm:git:https://github.com/line/lich.git'
                            developerConnection = 'scm:git:https://github.com/line/lich.git'
                            url = 'https://github.com/line/lich'
                        }
                        issueManagement {
                            system = 'GitHub'
                            url = 'https://github.com/line/lich/issues'
                        }
                    }
                }
            }
            repositories {
                maven {
                    url = findProperty('repositoryUrl') ?: project.version.endsWith('SNAPSHOT') ?
                        'https://oss.sonatype.org/content/repositories/snapshots/' :
                        'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    credentials {
                        username = findProperty('repositoryUsername') ?: ''
                        password = findProperty('repositoryPassword') ?: ''
                    }
                }
            }
        }
        signing {
            def signingKey = findProperty('signingKey')
            def signingPassword = findProperty('signingPassword')
            required { signingKey != null && signingPassword != null }
            useInMemoryPgpKeys(signingKey, signingPassword)
            sign publishing.publications.maven
        }
    }
}

nexusStaging {
    packageGroup = 'com.linecorp'
    stagingProfileId = '465b0f4eecd582'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
